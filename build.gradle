group 'rottenwood'
version '0.0.4'

apply plugin: 'java'
apply plugin: 'jacoco'

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'Main'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'artopia/exception/**')
        })
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

check.dependsOn jacocoTestReport
build.dependsOn fatJar


repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.powermock:powermock-mockito-release-full:1.+'
    compile 'org.hibernate:hibernate-core:5.0.6.Final'
    compile 'org.hibernate:hibernate-entitymanager:5.0.6.Final'
    compile 'mysql:mysql-connector-java:5.1.31'
    compile 'javax.transaction:jta:1.1'
    compile 'commons-codec:commons-codec:1.10'
}
